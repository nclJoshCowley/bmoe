var lp_regr[n_s, k], lp_wt[n_s, k];
data {
  # Ones trick
  ones = rep(1, n_s)
  C = 1000
}
model {
  # Transformed parameters
  lp_regr = x_regr %*% regr
  lp_wt = x_wt %*% wt

  for (i in 1:n_s) {
    for (ki in 1:k) {
      exp_lp_wt[i, ki] = exp(lp_wt[i, ki])
    }
  }

  # Likelihood
  for (i in 1:n_s) {
    ones[i] ~ dbern(L[i] / C)

    L[i] = ifelse(is_nd[i], y_cdf[i], y_pdf[i])
    y_cdf[i] = pnorm(y[i], mean[i], prec[z[i]])
    y_pdf[i] = dnorm(y[i], mean[i], prec[z[i]])

    mean[i] = lp_regr[i, z[i]]

    z[i] ~ dcat(probs[i, 1:k])
    probs[i, 1:k] = exp_lp_wt[i, 1:k]
  }

  # Prior
  for (ki in 1:k) { prec[ki] ~ dgamma(prec_shape, prec_rate) }

  for (j in 1:p_regr) {
    for (ki in 1:k) {
      regr[j, ki] ~ dnorm(0, regr_prec)
    }
  }

  for (j in 1:p_wt) {
    for (ki in 2:k) {
      wt[j, ki] ~ dnorm(0, wt_prec)
    }
    wt[j, 1] = 0
  }
}
